// room script file
PathNodeArray* pathNodeArr;
PathNode* pathNode;
PathNode* prevPathNode;
DynamicSprite* dynspr;
DrawingSurface* surf;
DrawingSurface* bgsurf;
DynamicSprite* bgspr;

bool eraser;
int prev_o_x;
int prev_o_y;
int prev_d_x;
int prev_d_y;


int prev_mouse_x;
int prev_mouse_y;
void doDrawPath(){  
  surf = dynspr.GetDrawingSurface();
  surf.Clear();
  surf.DrawingColor = 20032;
  
  pathNodeArr = AgsPathfinder.GetPathFromTo(obj_origin.X, obj_origin.Y, obj_destination.X, obj_destination.Y, 0);
  
 // Display("%d",pathNodeArr.Size);
  
  while(!pathNodeArr.Empty()){
    pathNode = pathNodeArr.Pop();
    if(prevPathNode!=null){
      surf.DrawLine(pathNode.X, pathNode.Y, prevPathNode.X, prevPathNode.Y);
    }
    prevPathNode = pathNode;
  }
  
  surf.Release();
  obj_img.Graphic = dynspr.Graphic;
  prevPathNode = null;
}

bool wasmousedown;
void repeatedly_execute_always (){ 
  if(prev_o_x != obj_origin.X || prev_o_y != obj_origin.Y ||
     prev_d_x != obj_destination.X || prev_d_y != obj_destination.Y ){
    doDrawPath();
    prev_o_x = obj_origin.X;
    prev_o_y = obj_origin.Y;
    prev_d_x = obj_destination.X; 
    prev_d_y = obj_destination.Y;
  } else {
    Object* obj = Object.GetAtRoomXY(mouse.x,  mouse.y);
    if(obj != null){
      return;  
    }
    
    if(mouse.IsButtonDown(eMouseLeft) && !wasmousedown){
      prev_mouse_x = mouse.x;
      prev_mouse_y = mouse.y;
    } else if(mouse.IsButtonDown(eMouseLeft)){
         
      bgsurf = Room.GetDrawingSurfaceForBackground();
      if(eraser){
        bgsurf.DrawingColor = 65535;
      } else {
        bgsurf.DrawingColor = 0;
      }
      bgsurf.DrawLine(prev_mouse_x, prev_mouse_y, mouse.x,  mouse.y, 2);
      bgsurf.Release();
      prev_mouse_x = mouse.x;
      prev_mouse_y = mouse.y;
    }  else if(!(mouse.IsButtonDown(eMouseLeft)) && wasmousedown){
      if(bgspr != null){
        bgspr.Delete();
        bgspr = null;
      }
      bgspr = DynamicSprite.CreateFromBackground();
      AgsPathfinder.SetGridFromSprite(bgspr.Graphic, 10);
      doDrawPath();
    }
  }
  
  wasmousedown = mouse.IsButtonDown(eMouseLeft);

}

function room_Load()
{
  DragDrop.Enabled = true;
  DragDropCommon.DragMove = eDDCmnMoveSelf;
  DragDropCommon.ModeEnabled[eDragDropRoomObject] = true;
  AgsPathfinder.SetGridFromSprite(1, 10);
  dynspr = DynamicSprite.Create(320, 180, true);
  
}


function obj_eraser_AnyClick()
{
  eraser = true; 
}

function obj_pen_AnyClick()
{
  eraser = false;
}
